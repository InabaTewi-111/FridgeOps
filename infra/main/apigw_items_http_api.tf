resource "aws_apigatewayv2_api" "items" {
  name          = "${local.project}-${local.env}-api"
  protocol_type = "HTTP"

  cors_configuration {
    allow_origins = ["*"]
    allow_methods = ["GET", "POST", "OPTIONS"]
    allow_headers = ["content-type"]
  }
}

resource "aws_apigatewayv2_integration" "items" {
  api_id                 = aws_apigatewayv2_api.items.id
  integration_type       = "AWS_PROXY"
  integration_uri        = aws_lambda_function.items.arn
  payload_format_version = "2.0"
  timeout_milliseconds   = 10000
}

resource "aws_apigatewayv2_route" "items_get" {
  api_id    = aws_apigatewayv2_api.items.id
  route_key = "GET /items"
  target    = "integrations/${aws_apigatewayv2_integration.items.id}"
}

resource "aws_apigatewayv2_route" "items_post" {
  api_id    = aws_apigatewayv2_api.items.id
  route_key = "POST /items"
  target    = "integrations/${aws_apigatewayv2_integration.items.id}"
}

resource "aws_apigatewayv2_stage" "default" {
  api_id      = aws_apigatewayv2_api.items.id
  name        = "$default"
  auto_deploy = true
}

resource "aws_lambda_permission" "apigw_invoke_items" {
  statement_id  = "AllowAPIGatewayInvokeItems"
  action        = "lambda:InvokeFunction"
  function_name = aws_lambda_function.items.function_name
  principal     = "apigateway.amazonaws.com"
  source_arn    = "${aws_apigatewayv2_api.items.execution_arn}/*/*"
}
